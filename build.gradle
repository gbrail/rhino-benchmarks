plugins {
    id 'application'
    id 'com.diffplug.spotless' version '8.1.0'
    id 'net.ltgt.errorprone' version '4.3.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation('org.mozilla:rhino:2.0.0-SNAPSHOT')
    implementation('info.picocli:picocli:4.7.7')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.21.0')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.14.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    errorprone 'com.google.errorprone:error_prone_core:2.45.0'
}



tasks.withType(Test) {
    useJUnitPlatform()
    maxHeapSize = '4g'
}

tasks.register('printDependencies') {
    def runtimeClasspath = configurations.runtimeClasspath
    doLast {
        runtimeClasspath.each {
            println it.absolutePath
        }
    }
}

application {
    mainClass = 'org.brail.rhinobenchmarks.Main'
    applicationDefaultJvmArgs = [
            "-XX:MaxGCPauseMillis=50",
            "-XX:-TieredCompilation",
            "-Xms4G",
            "-Xmx4G",
            "-Xbatch"]
}

tasks.register('compare', JavaExec) {
    mainClass = 'org.brail.rhinobenchmarks.Compare'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('profile', JavaExec) {
    mainClass = 'org.brail.rhinobenchmarks.Main'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = [
            "-XX:MaxGCPauseMillis=50",
            "-XX:-TieredCompilation",
            "-Xms4G",
            "-Xmx4G",
            "-Xbatch",
            "-XX:StartFlightRecording=filename=jfr.jfr"
            ]
}

spotless {
    java {
        googleJavaFormat('1.33.0')
        importOrder()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}