plugins {
    id 'java-library'
    id 'me.champeau.jmh' version '0.7.3'
    id 'com.diffplug.spotless' version '8.1.0'
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation ('org.mozilla:rhino:1.8.1')
}

jmh {
    if (System.getenv('BENCHMARK') != null) {
        includes = [System.getenv('BENCHMARK')]
    }
    if (System.getenv('INTERPRETED') != null) {
        def vals = System.getenv('INTERPRETED').split(",")
        def aParams = project.objects.listProperty(String)
        for (String v : vals) {
            aParams.add(v)
        }
        benchmarkParameters = [interpreted: aParams]
    }
    if (System.getenv('PROFILERS') != null) {
        if ('cpu'.equals(System.getenv('PROFILERS'))) {
            profilers = ['async:output=jfr']
        } else if ('alloc'.equals(System.getenv('PROFILERS'))) {
            profilers = ['async:output=jfr;event=alloc']
        } else {
            profilers = []
            for (String v :System.getenv('PROFILERS').split(',')) {
                profilers.add(v)
            }
        }
    }
    jvmArgs = ['-XX:+BackgroundCompilation']
    benchmarkMode = ['avgt']
    fork = 1
    iterations = 10
    timeOnIteration = '1s'
    warmupIterations = 5
    warmup = '5s'
}

spotless {
    // There is no version of googleJavaFormat that works for Java 11 and 17,
    // and different versions format differently. We're using a version that
    // requires at least Java 17.
    if (JavaVersion.current() >= JavaVersion.VERSION_17) {
        java {
            googleJavaFormat('1.23.0').aosp()
        }
    } else {
        System.out.println("Not running Spotless: Java language version is " + JavaVersion.current())
    }
}
